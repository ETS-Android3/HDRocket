apply plugin: 'com.android.application'

// 导入统一配置项
def info = rootProject.ext.android
def library = rootProject.ext.dependencies

android {
    compileSdkVersion info.compileSdkVersion
    defaultConfig {
        applicationId "com.de.rocket.app"
        minSdkVersion info.minSdkVersion
        targetSdkVersion info.targetSdkVersion
        versionCode info.versionCode
        versionName info.versionName
        multiDexEnabled info.multiDexEnabled
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    /*签名发布*/
    signingConfigs {
        release {
            storeFile file("../rocket.jks")
            storePassword "rocket"
            keyAlias "rocket"
            keyPassword "rocket"
        }
    }
    //Rease
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // 代码风格支持Java1.8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // 指定jniLibs库路径为libs
    sourceSets {
        main {
            jniLibs.srcDir 'libs'
        }
    }
    // 动态修改apk的名字
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def versionName = variant.versionName
            def versionCode = variant.versionCode
            def buildType = variant.buildType.name
            def time = new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+08:00"))
            if (variant.buildType.name == 'debug') {
                outputFileName = "rocket_app_v${versionName}_c${versionCode}_${buildType}.apk"
            } else {
                outputFileName = "rocket_app_v${versionName}_${time}_${buildType}.apk"
            }
        }
    }
}

//用户加载libs目录下的aar文件(统一将aar文件放在libs目录,api(name: 'rocket', ext: 'aar'))
repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    /* ******* 统一基础包 ******** */
    api fileTree(dir: 'libs', include: ['*.jar'])
    api library.v4
    api library.v7
    api library.multidex
    api library.recyclerview
    //api(name: 'rocket-v0.0.2', ext: 'aar')
    /* ******* 工程依赖 ******** */
    //api project(':rocket')
    /* ******* 第三方包 ******** */
    api 'com.github.yinhaide:Rocket-master:0.0.2'
}
